#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет эталонные (предопределенные) данные
//
Процедура ЗаполнитьЭталонныеДанные() Экспорт
	
	ЭталонныеДанные = ЭталонныеДанные();
	Для Каждого ТекущиеДанные Из ЭталонныеДанные Цикл
		СоздатьЭталонныйЭлемент(ТекущиеДанные);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭталонныеДанные()
	
	ТабличныйДокумент = Справочники.ПлатформенныеОбработки.ПолучитьМакет("ПредопределенныеДанные");
	
	ОбластьЯчеек   = ТабличныйДокумент.Область(1, 1, 
	ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);

    ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластьЯчеек);

    ПостроительОтчета = Новый ПостроительОтчета;
    ПостроительОтчета.ИсточникДанных = ИсточникДанных;
    ПостроительОтчета.Выполнить();

    Возврат ПостроительОтчета.Результат.Выгрузить();
	
КонецФункции

Процедура СоздатьЭталонныйЭлемент(ОписаниеДанных)
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор(ОписаниеДанных.УникальныйИдентификатор);
	ТекущаяСсылка = ПолучитьСсылку(УникальныйИдентификатор);
	Если СсылкаСуществует(ТекущаяСсылка) Тогда
		ТекущийОбъект = ТекущаяСсылка.ПолучитьОбъект();
	Иначе
		ТекущийОбъект = СоздатьЭлемент();
		ТекущийОбъект.УстановитьСсылкуНового(ТекущаяСсылка);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущийОбъект, ОписаниеДанных);
	
	ТекущийОбъект.ИменаПодключения.Очистить();
	КлючевыеИмена = СтрРазделить(ОписаниеДанных.КлючевыеИмена, "," + Символы.ПС, Ложь);
	Для Каждого КлючевоеИмя Из КлючевыеИмена Цикл
		ТекущийОбъект.ИменаПодключения.Добавить().КлючевоеИмя = СокрЛП(КлючевоеИмя);
	КонецЦикла;
	
	ТекущийОбъект.Записать();
	
КонецПроцедуры

Функция СсылкаСуществует(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	Справочник.ПлатформенныеОбработки
	|ГДЕ
	|	Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Возврат НЕ Запрос.Выполнить().Пустой();
		
КонецФункции

#КонецОбласти

#КонецЕсли