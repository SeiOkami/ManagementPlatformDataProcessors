// @strict-types

#Область ОбработчикиСобытий

// Обработка команды.
// 
// Параметры:
//  ПараметрКоманды - СправочникСсылка.ВерсииПлатформенныхОбработок
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ВыбратьФайлИСохранить(ПараметрКоманды);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выбрать файл и сохранить.
// 
// Параметры:
//  ВерсияИлиФайлы - СправочникСсылка.ВерсииПлатформенныхОбработок - Версия или файлы
//  ДополнительныеПараметры - СправочникСсылка.ВерсииПлатформенныхОбработок
&НаКлиенте
Процедура ВыбратьФайлИСохранить(ВерсияИлиФайлы, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(ВерсияИлиФайлы) = Тип("СправочникСсылка.ВерсииПлатформенныхОбработок") Тогда
		
		ДиалогВыбораФайла       = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогВыбораФайла.Фильтр = "Обработка (*.epf)|*.epf";
		ДиалогВыбораФайла.ПолноеИмяФайла = СтрШаблон("%1.epf", ВерсияИлиФайлы);
		ДополнительныеПараметры = ВерсияИлиФайлы;
		ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ВыбратьФайлИСохранить", ЭтотОбъект, ДополнительныеПараметры));
		
	ИначеЕсли ТипЗнч(ВерсияИлиФайлы) = Тип("Массив") И ЗначениеЗаполнено(ВерсияИлиФайлы) Тогда
		
		ИмяФайла    = ВерсияИлиФайлы[0]; //Строка
		ДанныеФайла = ДвоичныеДанныеВерсии(ДополнительныеПараметры);
		Если ТипЗнч(ДанныеФайла) = Тип("ДвоичныеДанные") Тогда
			ДанныеФайла.Записать(ИмяФайла);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Двоичные данные версии.
// 
// Параметры:
//  Версия - СправочникСсылка.ВерсииПлатформенныхОбработок
// 
// Возвращаемое значение:
//  ДвоичныеДанные
&НаСервере
Функция ДвоичныеДанныеВерсии(Версия)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Версия, "ДанныеВерсии").Получить();
	
КонецФункции

#КонецОбласти


