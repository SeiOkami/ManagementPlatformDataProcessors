
#Область СлужебныйПрограммныйИнтерфейс
 
// Версия приложения.
// 
// Возвращаемое значение:
//  Строка - Версия приложения
Функция ВерсияПриложения() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ВерсияПриложения;
	
КонецФункции

// Сообщить пользователю.
// 
// Параметры:
//  ТекстСообщенияПользователю - Строка
Процедура СообщитьПользователю(ТекстСообщенияПользователю) Экспорт
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщенияПользователю;
	Сообщение.Сообщить();
	
КонецПроцедуры

// Мультиязычное свойство объекта.
// 
// Параметры:
//  Объект - Произвольный - Объект, значение свойства которого необходимо получить
//  ИменаСвойств - Строка - Имена возможных имен свойств, перечисленных через запятую
// 
// Возвращаемое значение:
//  Произвольный, Неопределено - Значение свойства
Функция МультиязычноеСвойствоОбъекта(Объект, ИменаСвойств) Экспорт
	
	ВозможныеСвойства = Новый Структура(ИменаСвойств);
	ЗаполнитьЗначенияСвойств(ВозможныеСвойства, Объект);
	Для Каждого КлючИЗначение Из ВозможныеСвойства Цикл
		Если КлючИЗначение.Значение <> Неопределено Тогда
			Возврат КлючИЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
