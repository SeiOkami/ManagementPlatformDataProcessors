// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СейОбъект = РеквизитФормыВЗначение("Объект");
	СейОбъект.ЗаполнитьЭталонныеДанные();
	ЗначениеВРеквизитФормы(СейОбъект, "Объект");	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Команда_ВыгрузитьВыделенныеОбработки(Команда)
	
	ВыгрузитьВыделенныеОбработки();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ВыгрузитьВыделенныеОбработки()
	
	ВыделенныеСтроки = Элементы.ПлатформенныеОбработки.ВыделенныеСтроки;
	Если НЕ ЗначениеЗаполнено(ВыделенныеСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	РезультатВыбора = Ждать ДиалогВыбора.ВыбратьАсинх();
	Если НЕ ЗначениеЗаполнено(РезультатВыбора) Тогда
		Возврат;
	КонецЕсли;
	
	Каталог = РезультатВыбора[0]; //Строка
	
	ДанныеОбработок = ДанныеПлатформенныхОбработокДляВыгрузки(ВыделенныеСтроки);
	Для Каждого КлючИЗначение Из ДанныеОбработок Цикл
		
		ПолноеИмяФайла = СтрШаблон("%1\%2.epf", Каталог, КлючИЗначение.Ключ);
		КлючИЗначение.Значение.ЗаписатьАсинх(ПолноеИмяФайла);
		
	КонецЦикла;
	
КонецПроцедуры

// Данные платформенных обработок для выгрузки.
// 
// Параметры:
//  ВыделенныеСтроки - Массив из Число
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - ДвоичныеДанные
&НаСервере
Функция ДанныеПлатформенныхОбработокДляВыгрузки(ВыделенныеСтроки)
	
	Результат = Новый Соответствие;
	
	СейОбъект = РеквизитФормыВЗначение("Объект");
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		ВыделеннаяСтрока = Объект.ПлатформенныеОбработки.НайтиПоИдентификатору(ИдентификаторСтроки);
		КлючевыеИмена = СтрРазделить(ВыделеннаяСтрока.КлючевыеИмена, ",", Ложь);
		Для Каждого КлючевоеИмя Из КлючевыеИмена Цикл
			
			ПолныйПуть = СейОбъект.ПолныйПутьПлатформеннойОбработки(СокрЛП(КлючевоеИмя));
			ДвоичныеДанные = СейОбъект.ДвоичныеДанныеПлатформеннойОбработки(ПолныйПуть);
			Если ТипЗнч(ДвоичныеДанные) = Тип("ДвоичныеДанные") Тогда
				Результат.Вставить(ВыделеннаяСтрока.Наименование, ДвоичныеДанные);
				Продолжить;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
