// @strict-types

#Область ОбработчикиСобытий

// Обработка команды.
// 
// Параметры:
//  ПараметрКоманды - СправочникСсылка.ВерсииПлатформенныхОбработок
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды - :
//  * Источник - см. Справочник.ВерсииПлатформенныхОбработок.Форма.ФормаЭлемента
&НаКлиенте
Асинх Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если УправлениеПлатформеннымиОбработкамиВызовСервера.ЭтоОригинальнаяВерсия(ПараметрКоманды) Тогда
		ПоказатьПредупреждение(, "Нельзя изменять оригинальную версию обработки");
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла       = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр = "Обработка (*.epf)|*.epf";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	ВыбранныеФайлы = Ждать ДиалогВыбораФайла.ВыбратьАсинх();
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив") И ВыбранныеФайлы.Количество() > 0 Тогда
		ДанныеФайла = Новый ДвоичныеДанные(ВыбранныеФайлы[0]);
		ЗаписатьДанныеВерсии(ПараметрКоманды, ДанныеФайла);
		УправлениеПлатформеннымиОбработкамиКлиент.ОповеститьОбИзмененииВерсииПлатформенныхОбработок(ПараметрКоманды);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Записать данные версии.
// 
// Параметры:
//  ВерсияОбъекта - СправочникСсылка.ВерсииПлатформенныхОбработок
//  ДанныеВерсии - ДвоичныеДанные - Данные версии
&НаСервере
Процедура ЗаписатьДанныеВерсии(Знач ВерсияОбъекта, Знач ДанныеВерсии)
	
	Объект = ВерсияОбъекта.ПолучитьОбъект();
	Объект.ДанныеВерсии = Новый ХранилищеЗначения(ДанныеВерсии);
	Объект.Записать();
	
КонецПроцедуры

#КонецОбласти
