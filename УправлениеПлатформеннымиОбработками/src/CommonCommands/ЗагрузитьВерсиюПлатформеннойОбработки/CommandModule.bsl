// @strict-types

#Область ОбработчикиСобытий

// Обработка команды.
// 
// Параметры:
//  ПараметрКоманды - СправочникСсылка.ВерсииПлатформенныхОбработок
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОригинальнаяВерсия = ОригинальнаяВерсия(ПараметрКоманды);	
	Если НЕ ЗначениеЗаполнено(ОригинальнаяВерсия) Тогда
		ПоказатьПредупреждение(, "Нельзя изменять оригинальную версию обработки");
		Возврат;
	КонецЕсли;
	
	ВыбратьФайлИЗагрузить(ПараметрКоманды);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Оригинальная версия.
// 
// Параметры:
//  Ссылка - СправочникСсылка.ВерсииПлатформенныхОбработок
// 
// Возвращаемое значение:
//  СправочникСсылка.ВерсииПлатформенныхОбработок
Функция ОригинальнаяВерсия(Ссылка)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Родитель");
	
КонецФункции

// Выбрать файл и загрузить.
// 
// Параметры:
//  ВерсияИлиФайлы - СправочникСсылка.ВерсииПлатформенныхОбработок, Массив из Строка - Версия или файлы
//  ДополнительныеПараметры - Неопределено, СправочникСсылка.ВерсииПлатформенныхОбработок - Пустота или версия
&НаКлиенте
Процедура ВыбратьФайлИЗагрузить(ВерсияИлиФайлы, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(ВерсияИлиФайлы) = Тип("СправочникСсылка.ВерсииПлатформенныхОбработок") Тогда
		
		ДиалогВыбораФайла       = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбораФайла.Фильтр = "Обработка (*.epf)|*.epf";
		ДополнительныеПараметры = ВерсияИлиФайлы;
		ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ВыбратьФайлИЗагрузить", ЭтотОбъект, ДополнительныеПараметры));
		
	ИначеЕсли ТипЗнч(ВерсияИлиФайлы) = Тип("Массив") И ЗначениеЗаполнено(ВерсияИлиФайлы) Тогда
		
		ИмяФайла    = ВерсияИлиФайлы[0];
		ДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
		
		ЗаписатьДанныеВерсии(ДополнительныеПараметры, ДанныеФайла);
		
	КонецЕсли;
	
КонецПроцедуры

// Записать данные версии.
// 
// Параметры:
//  ВерсияОбъекта - СправочникСсылка.ВерсииПлатформенныхОбработок
//  ДанныеВерсии - ДвоичныеДанные - Данные версии
&НаСервере
Процедура ЗаписатьДанныеВерсии(ВерсияОбъекта, ДанныеВерсии)
	
	Объект = ВерсияОбъекта.ПолучитьОбъект();
	Объект.ДанныеВерсии = Новый ХранилищеЗначения(ДанныеВерсии);
	Объект.Записать();
	
КонецПроцедуры

#КонецОбласти

