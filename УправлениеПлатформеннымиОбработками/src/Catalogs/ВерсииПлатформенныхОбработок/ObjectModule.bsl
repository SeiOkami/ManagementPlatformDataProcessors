// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Обработка заполнения.
// 
// Параметры:
//  ДанныеЗаполнения - СправочникСсылка.ВерсииПлатформенныхОбработок
//  ТекстЗаполнения - Строка, Неопределено - Текст заполнения
//  СтандартнаяОбработка - Булево - Стандартная обработка
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ВерсииПлатформенныхОбработок") Тогда
		Возврат;
	КонецЕсли;
	
	Родитель = ДанныеЗаполнения;
	
	РеквизитыРодителя = РеквизитыРодителя();
		
	Если ЗначениеЗаполнено(РеквизитыРодителя.Родитель) Тогда
		Родитель = РеквизитыРодителя.Родитель;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыРодителя, "ДанныеВерсии,Владелец,ВерсияПриложения,КлючевоеИмя");
	
	Наименование = СтрШаблон("%1 (исправленная версия %2)", РеквизитыРодителя.Владелец, ВерсияПриложения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Реквизиты родителя.
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса -  Реквизиты родителя - :
//  * Родитель - СправочникСсылка.ВерсииПлатформенныхОбработок
//  * Владелец - СправочникСсылка.ПлатформенныеОбработки
Функция РеквизитыРодителя()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Владелец,
	|	ВерсияПриложения,
	|	Родитель,
	|	ДанныеВерсии,
	|	КлючевоеИмя
	|ИЗ
	|	Справочник.ВерсииПлатформенныхОбработок
	|ГДЕ
	|	Ссылка = &Родитель";
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка;
	
КонецФункции

#КонецОбласти

#КонецЕсли