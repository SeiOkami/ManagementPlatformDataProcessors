// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Родитель) 
		И ДанныеВерсии.Получить() = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Родитель, "ДанныеВерсии");
	КонецЕсли;		

КонецПроцедуры

// Обработка заполнения.
// 
// Параметры:
//  ДанныеЗаполнения - СправочникСсылка.УПО_ВерсииПлатформенныхОбработок
//  ТекстЗаполнения - Строка, Неопределено - Текст заполнения
//  СтандартнаяОбработка - Булево - Стандартная обработка
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.УПО_ВерсииПлатформенныхОбработок") Тогда
		Возврат;
	КонецЕсли;
	
	Родитель = ДанныеЗаполнения;
	
	РеквизитыРодителя = РеквизитыРодителя();
		
	Если ЗначениеЗаполнено(РеквизитыРодителя.Родитель) Тогда
		Родитель = РеквизитыРодителя.Родитель;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыРодителя, "ДанныеВерсии,Владелец,ВерсияПриложения,КлючевоеИмя");
	
	ЧастьПредставления = НСтр("ru='Измененная версия';en='Modified version'");
	Наименование = СтрШаблон("%1 (%2 %3)", РеквизитыРодителя.Владелец, ЧастьПредставления, ВерсияПриложения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Реквизиты родителя.
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса -  Реквизиты родителя - :
//  * Родитель - СправочникСсылка.УПО_ВерсииПлатформенныхОбработок
//  * Владелец - СправочникСсылка.УПО_ПлатформенныеОбработки
//  * ДанныеВерсии - ХранилищеЗначения
Функция РеквизитыРодителя()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Версии.Владелец КАК Владелец,
	|	Версии.ВерсияПриложения КАК ВерсияПриложения,
	|	Версии.Родитель КАК Родитель,
	|	Версии.ДанныеВерсии КАК ДанныеВерсии,
	|	Версии.КлючевоеИмя КАК КлючевоеИмя
	|ИЗ
	|	Справочник.УПО_ВерсииПлатформенныхОбработок КАК Версии
	|ГДЕ
	|	Версии.Ссылка = &Родитель";
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка;
	
КонецФункции

#КонецОбласти

#КонецЕсли