// @strict-types

#Область ОбработчикиСобытийФормы
 
// При создании на сервере.
// 
// Параметры:
//  Отказ - Булево - Отказ
//  СтандартнаяОбработка - Булево - Стандартная обработка
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВходящаяМодифицированность = Ложь;
	Если Параметры.Свойство("Модифицированность", ВходящаяМодифицированность) Тогда
		Модифицированность = ВходящаяМодифицированность;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	Если НЕ ЗначениеЗаполнено(Запись.Владелец) 
		И Параметры.Свойство("ЗначенияЗаполнения", ПараметрыЗаполнения) Тогда
		ПараметрыЗаполнения.Свойство("Владелец", Запись.Владелец);
	КонецЕсли;
	
	ОбновитьПредставлениеПользователя();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьПредставлениеПользователя();

КонецПроцедуры

// После записи.
// 
// Параметры:
//  ПараметрыЗаписи - Структура - Параметры записи
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	//@skip-check unknown-method-property
	Если НЕ ЗначениеЗаполнено(Запись.Владелец) 
		ИЛИ Запись.Владелец = УправлениеПлатформеннымиОбработкамиВызовСервера.ИдентификаторТекущегоПользователяИБ() Тогда
		
		УправлениеПлатформеннымиОбработкамиКлиент.СпроситьИПодключитьВерсиюОбработкиДляТекущегоСеанса(Запись.ВерсияОбработки);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьПредставлениеПользователя();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеПользователяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИмяФормыВыбора = ИмяФормыВыбораПользователя();
	
	ПараметрыВыбора = Новый Структура();
	ПараметрыВыбора.Вставить("РежимВыбора", Истина);
	
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыВыбора, Элементы.ПредставлениеПользователя);

КонецПроцедуры

// Представление пользователя обработка выбора.
// 
// Параметры:
//  Элемент - ПолеФормы -  Элемент
//  ВыбранноеЗначение - УникальныйИдентификатор, Структура - :
//  * ИдентификаторПользователяИБ - УникальныйИдентификатор
//  СтандартнаяОбработка - Булево -  Стандартная обработка
&НаКлиенте
Процедура ПредставлениеПользователяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УстановитьНовогоВладельца(ВыбранноеЗначение);
	
	ОбновитьПредставлениеПользователя();

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПользователяОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Запись.Владелец = Неопределено;
	ОбновитьПредставлениеПользователя();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Установить нового владельца.
// 
// Параметры:
//  ВыбранноеЗначение - см. ПредставлениеПользователяОбработкаВыбора.ВыбранноеЗначение
&НаСервере
Процедура УстановитьНовогоВладельца(Знач ВыбранноеЗначение)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("УникальныйИдентификатор") Тогда
	
		Запись.Владелец = ВыбранноеЗначение;
	
	ИначеЕсли ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		Идентификатор = УправлениеПлатформеннымиОбработкамиКлиентСервер.МультиязычноеСвойствоОбъекта(
			ВыбранноеЗначение, "ИдентификаторПользователяИБ,IBUserID"); // УникальныйИдентификатор
		Запись.Владелец = Идентификатор;
		
	КонецЕсли; // BSLLS:IfElseIfEndsWithElse-off
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеПользователя()
	
	ДанныеПредставления = УправлениеПлатформеннымиОбработками.ПредставлениеПользователяИБ(Запись.Владелец);
	ПредставлениеПользователя = ДанныеПредставления.Текст;
	Элементы.ПредставлениеПользователя.ЦветТекста = ДанныеПредставления.ЦветТекста;
	Элементы.Владелец.ЦветТекста = ДанныеПредставления.ЦветТекста;
	Элементы.Владелец.Видимость = ЗначениеЗаполнено(Запись.Владелец);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяФормыВыбораПользователя()
	
	ВозможныеКаталогиПользователей = СтрРазделить("Пользователи,Users", ",");
	Для Каждого ВозможныйКаталог Из ВозможныеКаталогиПользователей Цикл
		МетаданныеКаталога = Метаданные.Справочники.Найти(ВозможныйКаталог);
		Если МетаданныеКаталога <> Неопределено Тогда
			Возврат МетаданныеКаталога.ОсновнаяФормаДляВыбора.ПолноеИмя();
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Метаданные.РегистрыСведений.ПодменаПлатформенныхОбработок.Формы.ФормаВыбораПользователяИБ.ПолноеИмя();
	
КонецФункции

#КонецОбласти
